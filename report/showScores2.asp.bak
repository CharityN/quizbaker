<%Option Explicit%>
<!--#include file="adovbs.asp"-->
<!--#include file="utils.asp"-->
<!--#include file="debug.asp"-->
<!--#include file="database.asp"-->
<!--#include file="quizdata.asp"-->
<!--#include file="quiz.asp"-->
<%
Dim oQuiz
Dim nl
Dim c
Dim br

c = ","
nl = vbNewLine
br = "<br/>"
set oQuiz = new Quiz

' Main

if Req("view")="" then
	if Req("details")="true" then
		saveDetails
	else
		saveScores
	end if
end if

' Routines
Sub displayDetails
	Dim db
	Dim rs
	Dim sql
	Dim quizName
	Dim strImagePath

	' init db connection
	set db = oQuiz.getDB()

	quizName = oQuiz.GetName(Req("id"))

	'  get details
	sql = "SELECT * FROM vwDetails "
	sql = sql & "WHERE quizId=" & Req("id") & " "
	if Req("student")<>"" then
		sql = sql & "AND Nummer=" & sq(Req("student")) & " "
	end if
	sql = sql & "ORDER BY timestamp DESC"
	set rs = db.getRs(sql, adOpenForwardOnly, adLockReadOnly)

	strImagePath = getImagePath(rs)

	%>
	<h2>Scores voor quiz: <strong><%=quizname%></strong></h2>
	<%=anchor(strImagePath, img(strImagePath, 120), "lightbox")%>

	<dl>
		<dt>ID</dt><dd><%=rs("Nummer")%></dd>
		<dt>Student</dt><dd><%=rs("Achternaam")%>, <%=rs("Voornaam")%></dd>
		<dt>Klas</dt><dd><%=rs("Klas")%></dd>
		<dt>Percentage</dt><dd><%=rs("raw_score")%>%</dd>
	</dl>
	<table>
	<tr>
		<th>questionNum</th>
		<th>question</th>
		<th>response</th>
		<th>result</th>
		<th>score</th>
		<th>type</th>
	</tr>
	<%
	do until rs.eof
		print "<tr>"
		print td(rs("questionNum")) & nl
		print td(rs("question")) & nl
		print td(rs("student_response")) & nl
		print td(rs("result")) & nl
		print td(rs("score")) & nl
		print td(rs("interaction_type")) & nl
		print "</tr>"
		rs.MoveNext
	loop
	%></table><%
	rs.close
	db.CloseConn
	set rs = nothing
	set db = nothing
End Sub

Sub displayScores(klas)
	Dim db
	Dim rs
	Dim sql
	Dim quizName

	REM init db connection
	set db = oQuiz.getDB()

	if Req("id")<>"" then
		quizName = oQuiz.GetName(Req("id"))
	end if

	' get scores
	sql = "SELECT * FROM vwSummary "
	sql = sql & " WHERE 1=1 "
	if Req("id")<>"" then
		sql = sql & "AND quiz_id=" & Req("id")
	end if
	if klas<>"" then
		sql = sql & "AND Klas=" & sq(klas)
	end if
	sql = sql & " ORDER BY lastmodified DESC"
	set rs = db.getRs(sql, adOpenForwardOnly, adLockReadOnly)

	%>
	<h2>Scores voor quiz: <strong><%=quizname%></strong></h2>

	<%
	If LCase(Req("style")) = "pictures" Then
		ShowScoresPictures rs
	Else
		ShowScoresList rs
	End If

	rs.close
	db.CloseConn
	set rs = nothing
	set db = nothing
End Sub

Sub ShowScoresList(rs)
	Dim strImagePath
	%>
	<table>
	<tr>
		<th>photo</th>
		<th>voornaam</th>
		<th>achternaam</th>
		<th>klas</th>
		<th>student</th>
		<th>status</th>
		<th>score</th>
		<th>time</th>
		<th>view</th>
	</tr>
	<%
	do until rs.eof
		strImagePath = getImagePath(rs)

		print "<tr>"
		print td(anchor(strImagePath, img(strImagePath, 60), "lightbox")) & nl
		print td(rs("Voornaam")) & nl
		print td(rs("Achternaam")) & nl
		print td(rs("Klas")) & nl
		%><td><a href="showStudent.asp?id=<%print rs("Nummer")%>"><%=rs("Nummer")%><a/></td><%
		print td(rs("status")) & nl
		print td(rs("raw_score")) & nl
		print td(rs("time"))
		%><td><a href="showScores.asp?view=true&details=true&id=<% print rs("quiz_id")%>&student=<%print rs("Nummer")%>">view<a/></td><%
		print vbNewLine
		print "</tr>"
		rs.MoveNext
	loop
	%></table><%
End Sub

Sub showScoresPictures(rs)
	Dim strImagePath
	Dim strTitle

	%>
	<div id="photobook">
	<%
	do until rs.eof
		strImagePath = getImagePath(rs)

		%><div><%
		print anchor(strImagePath, img(strImagePath, 120), "lightbox") & nl
		%><dl class="tooltip"><%
		print dt(rs("quiz_id")) & nl
		print dt("Voornaam") & dd(rs("Voornaam")) & nl
		print dt("Achternaam") & dd(rs("Achternaam")) & nl
		print dt("Klas") & dd(rs("Klas")) & nl
		print dt("Nummer") & dd(rs("Nummer")) & nl
		print dt("Status") & dd(rs("status")) & nl
		print dt("Score") & dd(rs("raw_score")) & nl

		strTitle = rs("Voornaam") & " " & rs("Achternaam") & br
		strTitle = strTitle & "Klas: " & rs("Klas") & br
		strTitle = strTitle & "Nummer: " & rs("Nummer") & br
		strTitle = strTitle & "Status: " & rs("status") & br
		strTitle = strTitle & "Score: " & rs("raw_score") & br
		%></dl>

		<p><a href="showScores.asp?view=true&details=true&id=<% print rs("quiz_id")%>&student=<%print rs("Nummer")%>">details<span class="tooltip"><span></span><%=strTitle%></span><a/></p><%
		print vbNewLine
		print "</div>"
		rs.MoveNext
	loop
	%></table><%
End Sub

Sub saveScores()
	Dim db
	Dim rs
	Dim sql

	Response.ContentType = "text/csv"
	Response.AddHeader "Content-Disposition","attachment; filename=scores.csv"

	set db = oQuiz.getDB()

	sql = "SELECT * FROM vwSummary WHERE quiz_id=" & Req("id")& " ORDER BY lastmodified DESC"
	set rs = db.getRs(sql, adOpenForwardOnly, adLockReadOnly)
	print "id,voornaam,achternaam,klas,student,status,score,time" & vbNewline
	do until rs.eof
		print q(rs("quiz_id")) & c
		print q(rs("Voornaam")) & c
		print q(rs("Achternaam")) & c
		print q(rs("Klas")) & c
		print q(rs("Nummer")) & c
		print q(rs("status")) & c
		print q(rs("raw_score")) & c
		print q(rs("time"))
		print vbNewLine
		rs.MoveNext
	loop
	rs.close
	db.CloseConn
	set rs = nothing
	set db = nothing
	response.end
end sub

sub saveDetails
	Dim db
	Dim rs
	Dim sql

	Response.ContentType = "text/csv"
	Response.AddHeader "Content-Disposition","attachment; filename=details.csv"

	set db = oQuiz.getDB()

	sql = "SELECT * FROM [Toets].[dbo].[vwDetails] WHERE quizId=" & Req("id") & " ORDER BY timestamp DESC"
	set rs = db.getRs(sql, adOpenForwardOnly, adLockReadOnly)
	print "quizid,student,question_number,question,response,result,score,percentage,type,last,first,class" & vbNewLine
	do until rs.eof
		print q(rs("quizId")) & c
		print q(rs("Nummer")) & c
		print q(rs("questionNum")) & c
		print q(rs("question")) & c
		print q(rs("student_response")) & c
		print q(rs("result")) & c
		print q(rs("score")) & c
		print q(rs("raw_score")) & c
		print q(rs("interaction_type")) & c
		print q(rs("Achternaam")) & c
		print q(rs("Voornaam")) & c
		print q(rs("Klas"))
		print vbNewLine
		rs.MoveNext
	loop
	rs.close
	db.CloseConn
	set rs = nothing
	set db = nothing
	response.end
End Sub

Function getImagePath(rs)
	Dim strFilePath
	Dim strImagePath

	if nvl(rs("Photo"))<>"" then
		strFilePath = nvl(rs("Photo"))
	else
		strFilePath = nvl(rs("Nummer")) & ".jpg"
	end if
	strImagePath = "\students\" & strFilePath
	if not FileExists(strImagePath) then
		strImagePath = "/students/avatar.gif"
	end if
	getImagePath = replace(strImagePath, "\", "/")
End Function

Sub cleanup()
	Dim db
	Dim rs
	Dim sql

	set db = oQuiz.getDB()

	sql = "SELECT * FROM Quiz_Summary WHERE status=''"
	set rs = db.getRs(sql, adOpenForwardOnly, adLockReadOnly)
	do until rs.eof
		sql = "DELETE FROM Quiz_Detail WHERE summary_id=" & rs("ID")
		db.execute(sql)
		sql = "DELETE FROM Quiz_Summary WHERE ID=" & rs("ID")
		db.execute(sql)
		rs.MoveNext
	loop
	sql = "DELETE FROM Quiz WHERE quizname = ''"
	db.execute(sql)
	rs.close
	db.CloseConn
	set rs = nothing
	set db = nothing

End Sub
%>

<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN" "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">
<html xmlns="http://www.w3.org/1999/xhtml" dir="ltr" lang="en-US">

<head profile="http://gmpg.org/xfn/11">
<meta http-equiv="Content-Type" content="text/html; charset=UTF-8" />

<title>Resultaten</title>
<link rel="stylesheet" type="text/css" href="/report/lightbox/css/jquery.lightbox-0.5.css" media="screen" />
<link rel="stylesheet" type="text/css" href="tooltips.css" media="screen" />
<link rel="stylesheet" type="text/css" href="/report/main.css" media="screen" />

<script type="text/javascript" src="/DB/jquery.min.js"></script>
<script type="text/javascript" src="/report/lightbox/js/jquery.lightbox-0.5.js"></script>

<script>
$(function() {
	$('a.lightbox').lightBox();
});
</script>
<style>
body { font: .76em arial; margin:1em; }
td, th {border:1px solid silver; padding:.2em}
table {border-collapse:collapse}
tr:nth-child(odd) { background-color: #e2f7f7;}
dl {margin: 0;	padding: 0;}
dt {margin: 0;  padding: 0;	font-weight: bold;}
dd {margin: 0 0 1em 0;	padding: 0;}

a img {	padding:3px; border:solid; border-color: #dddddd #aaaaaa #aaaaaa #dddddd; border-width: 1px 2px 2px 1px;background-color:white; }
#photobook a { text-decoration: none; width:100%; }
#photobook a span { text-align:left; }
#photobook p { margin:0; text-align:center}
#photobook dl {	display:none }
#photobook div { float:left; margin:.2em; }
#credits { position:absolute; top:2em; right:15em }
</style>
</head>
<body>
<p id="credits"><a href="http://about.me/michiel">Help<span class="tooltip"><span></span>Vragen? Email Michiel van der Blonk : pmvanderblonk@epiaruba.com</span></a></p>
<a href="/report/"><img src="logoEPI.png" width="200"/></a>
<h1>Resultaten</h1>
<%
cleanup()

if Req("view")="true" then
	if Req("class") <> "" then
		displayScores Req("class")
	else
		if Req("details")="true" then
			displayDetails
		else
			%>
			<form name="style" method="get">
			<input type="hidden" name="view" value="<%=Req("view")%>" />
			<input type="hidden" name="id" value="<%=Req("id")%>" />
			<button type="submit" name="style" value="pictures">Pictures</button>
			<button type="submit" name="style" value="list">List</button>
			</form>
			<%
			displayScores ""
		end if
	end if
end if
%>

</body>
</html>
